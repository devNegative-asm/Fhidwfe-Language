guard

//specifically allow these variables to be accessible in other files
import heap
import heaptail
import null
import one
import int_size

//alias uint sizeof(a:listbyte)
//alias uint sizeof(a:listubyte)
//alias uint sizeof(a:listptr)
//alias uint sizeof(a:listuint)
//alias uint sizeof(a:listint)

alias void put_byte(a:ptr b:ubyte)//don't penalize for not using the right one
alias void put_ubyte(a:ptr b:byte)


function uint sizeof(input:ptr) {
	return deref_uint$ - input as ? 1 ptr
}

//memory is held as a linked list with dummy nodes at the beginning and end
//if there is room somewhere in the list, put each new object there

// ? n is the same as n*int_size

function ptr malloc(size:uint) {
	curobj = heap
	space = as + size ? 3u ptr
	//need space for my object + 3 ints
	
	next = deref_ptr$ - curobj as ? 2u ptr
	blocksize = deref_ptr$ - curobj as int_size ptr
	
	while < - - next curobj blocksize space {
		curobj = next
		next = deref_ptr$ - curobj as ? 2u ptr
		blocksize = deref_ptr$ - curobj as int_size ptr
		
		if = next null {
			return null//no room
		} ;
		
	}
	
	newobjectloc = + + curobj blocksize as ? 3u ptr
	
	//header for the new object
	put_uint$ - newobjectloc as int_size ptr size
	put_ptr$ - newobjectloc as ? 2u ptr next
	put_ptr$ - newobjectloc as ? 3u ptr curobj
	
	//update previous object's header
	put_ptr$ - curobj as ? 2u ptr newobjectloc
	
	//update next object's header
	put_ptr$ - next as ? 3u ptr newobjectloc
	
	return newobjectloc
	
}

//UNTESTED
function ptr realloc(block:ptr newsize:uint) {
	if <= newsize sizeof$ block {
		//remove space from this block
		put_uint$ - block as ?1 ptr newsize
		return block
	} ;
	next = deref_ptr$ - block as ? 2u ptr
	
	//easiest when there's extra room between objects
	//space needed between ptr and next is newsize + 3
	
	if <= newsize - as - next block uint ? 3u {
		put_uint$ - block as ?1 ptr newsize
		return block
	} ;
	
	//there isn't enough room to simply extend it
	newloc = malloc$ newsize
	//memcpy the minimum number of bytes to assure no buffer overflow
	memcpy$ newloc block umin$ sizeof$ block newsize
	free$ block
	return newloc
}



alias void free(a:listbyte)
alias void free(a:listubyte)
alias void free(a:listptr)
alias void free(a:listuint)
alias void free(a:listint)
alias void free(a:listfunc)
alias void free(a:listop)
alias void free(a:range)
alias void free(a:urange)
alias void free(a:brange)
alias void free(a:ubrange)
function void free(block:ptr) {
	if || || = block null < block heap >= block heaptail {
		return
	} ;
	
	prev = deref_ptr$ - block as ? 3u ptr
	next = deref_ptr$ - block as ? 2u ptr
	
	put_ptr$ - prev as ? 2u ptr next
	put_ptr$ - next as ? 3u ptr prev
}
